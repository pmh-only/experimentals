plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.11.2'
}

group = 'codes.pmh.mc'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

remotes {
    shutupandtakemycodes {
        host = 'shutupandtakemy.codes'
        user = 'pmh'
        port = 2202
        identity = file("${System.properties['user.home']}/.ssh/id_ecdsa")
    }
}

tasks.register("deploy") {
    dependsOn("build")

    doLast {
        ssh.run {
            session(remotes.shutupandtakemycodes) {
                put from: "${project.projectDir}/build/libs/experimentals-1.0-SNAPSHOT.jar", into: "composes/minecraft/plugindev/plugins/experimentals.jar"
            }
        }
    }
}
